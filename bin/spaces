#!/usr/bin/env perl
use v5.12;
use warnings;

use autodie;
use File::Copy qw(move);

# spaces.pl path/to/dir spaces_num
my $dir = shift;
my $spaces = shift() // 4;
my $spaces_str = ' ' x $spaces;

sub fix
{
	my ($filename) = @_;
	#
	# 2 MB threshold
	return if -s $filename > (2 << 20);

	say "fixing $filename";
	{
		open my $fread, '<', $filename;
		open my $fwrite, '>', "$filename.fixed";

		while (my $line = readline $fread) {
			1 while ($line =~ s/^(\t*)$spaces_str/$1\t/); # fix indentation
			$line =~ s/[ ]{2,}/ /g; # fix multiple spaces
			print {$fwrite} $line;
		}
	}

	move "$filename.fixed", $filename;
	return;
}

sub recurse
{
	my ($dir) = @_;

	for my $file (glob "$dir/*") {
		if (-d $file) {
			recurse($file);
		}
		else {
			fix($file);
		}
	}
	return;
}

if (-d $dir) {
	recurse($dir);
}
else {
	fix($dir);
}

